IF
	Global("Ys_TalkedToBandit","GLOBAL",1)
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("ys_TalkBypass","LOCALS",1)
		Enemy()
		Continue()
END

IF
	Global("ys_TalkBypass","LOCALS",0)
	See([PC])
THEN
	RESPONSE #100
		Dialogue(Protagonist)
END

//Find a target
IF
  Allegiance(Myself,ENEMY)
	OR(2)
		See([NEUTRAL])
		See([GOODCUTOFF])
	!Class(LastSeenBy(),INNOCENT)
THEN
	RESPONSE #100
		Continue()
END

//Fighting script
IF
	OR(4)
		AttackedBy([ANYONE],DEFAULT)
		AttackedBy([ANYONE],MELEE)
		AttackedBy([ANYONE],RANGED)
		Global("ys_TalkedToTentGuard","GLOBAL",2)
  !Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
	DisplayStringHead(Myself,@1829)
    Enemy()
END

//If I'm being attacked then select my attacker as a new target
IF
  Allegiance(Myself,ENEMY)
	OR(3)
		AttackedBy([ANYONE],DEFAULT)
		AttackedBy([ANYONE],MELEE)
		AttackedBy([ANYONE],RANGED)
	See(LastAttackerOf(Myself))
THEN
	RESPONSE #100
		Continue()
END

//Select weapon
IF
  Allegiance(Myself,ENEMY)
	!Range(LastSeenBy(Myself),5)
THEN
	RESPONSE #100
		EquipRanged()
		Attack(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
	Range(LastSeenBy(Myself),5)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		Attack(LastSeenBy(Myself))
END

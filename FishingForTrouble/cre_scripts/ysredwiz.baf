//Red Wizard attacks and defences
//Non-Ailoth
IF
	Global("ys_GarrisonFight","GLOBAL",1)
	Global("ys_TurnMeHostile","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("ys_TurnMeHostile","LOCALS",1)
		Enemy()
END

IF
	AttackedBy([GOODCUTOFF],DEFAULT)
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Enemy()
END

//When I'm killed update the total number of dead Red Guards
IF
  Dead("ysredwiz")
  Global("ys_CheckDead","LOCALS",0)
THEN
  RESPONSE #100
    IncrementGlobal("ys_DeadRedGuards","GLOBAL",1)
    SetGlobal("ys_CheckDead","LOCALS",1)
END

//Select some magic defences
IF
	Allegiance(Myself,ENEMY)
	Global("ys_RWGI","LOCALS",0)
THEN
	RESPONSE #14
		SetGlobal("ys_RWGI","LOCALS",1)
		ReallyForceSpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
		ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
	RESPONSE #28
		SetGlobal("ys_RWGI","LOCALS",1)
		ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_BLUE)
		ReallyForceSpell(Myself,WIZARD_ARMOR)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
	RESPONSE #28
		SetGlobal("ys_RWGI","LOCALS",1)
		ReallyForceSpell(Myself,WIZARD_GHOST_ARMOR)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
		ReallyForceSpell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
	RESPONSE #30
		SetGlobal("ys_RWGI","LOCALS",1)
		ReallyForceSpell(Myself,WIZARD_MINOR_SPELL_DEFLECTION)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
	Continue()
END

//Throw out an area spell
IF
	Allegiance(Myself,ENEMY)
	Global("ys_RWWeb","LOCALS",0)
  OR(2)
		See([GOODCUTOFF])
		See([NEUTRAL])
THEN
	RESPONSE #33
		SetGlobal("ys_RWWeb","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_WEB)
	RESPONSE #67
		SetGlobal("ys_RWWeb","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_GREASE)
	Continue()
END

//Give them a selection of spells to chose from
IF
	Allegiance(Myself,ENEMY)
	GlobalLT("ys_RWMM","LOCALS",10)
  OR(2)
		See([GOODCUTOFF])
		See([NEUTRAL])
THEN
	RESPONSE #5
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
	RESPONSE #10
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_CHROMATIC_ORB)
	RESPONSE #10
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_AGANNAZAR_SCORCHER)
	RESPONSE #10
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_DIRE_CHARM)
	RESPONSE #10
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_EMOTION_HOPELESSNESS)
	RESPONSE #10
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
	RESPONSE #10
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
	RESPONSE #10
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_ANIMATE_DEAD)
	RESPONSE #10
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_HORROR)
	RESPONSE #10
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_CONFUSION)
	RESPONSE #5
		IncrementGlobal("ys_RWMM","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_LARLOCH_MINOR_DRAIN)
END

IF
	Allegiance(Myself,ENEMY)
	GlobalGT("ys_RWMM","LOCALS",10)
  OR(2)
		See([GOODCUTOFF])
		See([NEUTRAL])
THEN
	RESPONSE #100
		RunAwayFrom([GOODCUTOFF],75)
END

